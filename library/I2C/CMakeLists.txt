set(DEFAULT_ADS1015_GAIN ADS1015_CONFIG_PGA_4096MV CACHE STRING "Default ADS1015 PGA Value")

configure_file("${CMAKE_CURRENT_SOURCE_DIR}/I2C-ADS1015.h.in" "${CMAKE_CURRENT_BINARY_DIR}/I2C-ADS1015.h" @ONLY)

list(APPEND SL_I2C_SRC I2CConnection.c I2C-INA219.c I2C-ADS1015.c I2C-SN3218.c)
list(APPEND SL_I2C_INC I2CConnection.h I2C-INA219.h "${CMAKE_CURRENT_BINARY_DIR}/I2C-ADS1015.h" I2C-SN3218.h)

add_library(SELKIELoggerI2C ${SL_I2C_SRC})
set_target_properties(SELKIELoggerI2C PROPERTIES VERSION ${PROJECT_VERSION})
set_target_properties(SELKIELoggerI2C PROPERTIES SONAME 1)

set_target_properties(SELKIELoggerI2C PROPERTIES PUBLIC_HEADER SELKIELoggerI2C.h)
set_target_properties(SELKIELoggerI2C PROPERTIES PRIVATE_HEADER "${SL_I2C_INC}")

find_library(I2CTools NAMES i2c REQUIRED)
target_link_libraries(SELKIELoggerI2C PUBLIC ${I2CTools})

include(GNUInstallDirs)

target_include_directories(SELKIELoggerI2C PUBLIC
	$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/library>
	$<BUILD_INTERFACE:${PROJECT_BINARY_DIR}/library>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger>)
target_include_directories(SELKIELoggerI2C PRIVATE
	$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/library>
	$<BUILD_INTERFACE:${PROJECT_BINARY_DIR}/library>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger>)

install(TARGETS SELKIELoggerI2C
	LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT I2C
	PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger COMPONENT I2CDev
	PRIVATE_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger/I2C COMPONENT I2CDev
	)

set(CPACK_COMPONENT_I2C_GROUP "Libraries")
set(CPACK_COMPONENT_I2C_DISPLAY_NAME "SELKIE I2C Library")
set(CPACK_COMPONENT_I2CDEV_GROUP "Headers")
set(CPACK_COMPONENT_I2CDEV_DISPLAY_NAME "SELKIE I2C Library - Headers")
set(CPACK_COMPONENT_I2CDEV_DEPENDS I2C)
