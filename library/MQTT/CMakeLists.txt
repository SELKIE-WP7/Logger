list(APPEND SL_MQTT_SRC MQTTConnection.c)
list(APPEND SL_MQTT_INC MQTTTypes.h MQTTConnection.h)

add_library(SELKIELoggerMQTT ${SL_MQTT_SRC})
set_target_properties(SELKIELoggerMQTT PROPERTIES VERSION ${PROJECT_VERSION})
set_target_properties(SELKIELoggerMQTT PROPERTIES SONAME 1)

set_target_properties(SELKIELoggerMQTT PROPERTIES PUBLIC_HEADER SELKIELoggerMQTT.h)
set_target_properties(SELKIELoggerMQTT PROPERTIES PRIVATE_HEADER "${SL_MQTT_INC}")

# find_package(Mosquitto)
find_package(PkgConfig REQUIRED)
pkg_check_modules(Mosquitto REQUIRED IMPORTED_TARGET libmosquitto)

target_link_libraries(SELKIELoggerMQTT PUBLIC SELKIELoggerBase PkgConfig::Mosquitto)

include(GNUInstallDirs)

target_include_directories(SELKIELoggerMQTT PUBLIC
	$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/library>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger>)
target_include_directories(SELKIELoggerMQTT PRIVATE
	$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/library>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger>)

install(TARGETS SELKIELoggerMQTT
	EXPORT SLPubLibs
	LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT MQTT
	PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger COMPONENT MQTTDev
	PRIVATE_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger/MQTT COMPONENT MQTTDev
	)

set(CPACK_COMPONENT_MQTT_GROUP "Libraries")
set(CPACK_COMPONENT_MQTT_DISPLAY_NAME "SELKIE MQTT Library")
set(CPACK_COMPONENT_MQTTDEV_GROUP "Headers")
set(CPACK_COMPONENT_MQTTDEV_DISPLAY_NAME "SELKIE MQTT Library - Headers")
set(CPACK_COMPONENT_MQTTDEV_DEPENDS MQTT)
