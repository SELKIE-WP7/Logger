list(APPEND SL_N2K_SRC N2KTypes.c N2KConnection.c)
list(APPEND SL_N2K_INC N2KTypes.h N2KConnection.h)

add_library(SELKIELoggerN2K ${SL_N2K_SRC})
set_target_properties(SELKIELoggerN2K PROPERTIES VERSION ${PROJECT_VERSION})
set_target_properties(SELKIELoggerN2K PROPERTIES SONAME 1)

set_target_properties(SELKIELoggerN2K PROPERTIES PUBLIC_HEADER SELKIELoggerN2K.h)
set_target_properties(SELKIELoggerN2K PROPERTIES PRIVATE_HEADER "${SL_N2K_INC}")

target_link_libraries(SELKIELoggerN2K PUBLIC SELKIELoggerBase)

include(GNUInstallDirs)

target_include_directories(SELKIELoggerN2K PUBLIC
	$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/library>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger>)
target_include_directories(SELKIELoggerN2K PRIVATE
	$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/library>
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger>)

install(TARGETS SELKIELoggerN2K
	EXPORT SLPubLibs
	LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT N2K
	PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger COMPONENT N2KDev
	PRIVATE_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/SELKIELogger/N2K COMPONENT N2KDev
	)

set(CPACK_COMPONENT_N2K_GROUP "Libraries")
set(CPACK_COMPONENT_N2K_DISPLAY_NAME "SELKIE N2K Library")
set(CPACK_COMPONENT_N2KDEV_GROUP "Headers")
set(CPACK_COMPONENT_N2KDEV_DISPLAY_NAME "SELKIE N2K Library - Headers")
set(CPACK_COMPONENT_N2KDEV_DEPENDS N2K)
